#
# GAWK script to extract both path and submodule URI definitions from a .gitmodules file.
#
#
# [submodule "php/lib/ultimatemysql"]
# 	 path = php/lib/ultimatemysql
# 	 url = git@github.com:Visyond/ultimatemysql.git
#
# -->
#
# git submodule add  git@github.com:Visyond/ultimatemysql.git  php/lib/ultimatemysql
#


BEGIN {
	printf("#! /bin/bash\n");
	printf("# generated by collect_git_add_recursively.sh\n");
	printf("\n");
	printf("pushd $(dirname $0)                                                             						2> /dev/null   > /dev/null\n");
	printf("cd ..\n");
	printf("\n");
	printf("\n");
	printf("\n");
	printf("\n");

	state = 0;
	idx = 0;
}

/\[submodule/	{
	# because MSys gawk doesn't support match() with 3 arguments :-((
	split($0, a, "\"");
	submodule_path = a[2];
	#printf("Selecting path [%s]\n", submodule_path);
	next;
}

/path = /	{
	# because MSys gawk doesn't support match() with 3 arguments :-((
	split($0, a, "=");
	submodule_path = a[2];
	#printf("Selecting path [%s]\n", submodule_path);
	next;
}

/url = /	{
	# because MSys gawk doesn't support match() with 3 arguments :-((
	split($0, a, "=");
	submodule_uri = a[2];
	#printf("Selecting URI [%s]\n", submodule_uri);

	stmts[++idx] = sprintf("git submodule add  %-70s  %s", submodule_uri, submodule_path);
	#printf("# id %d: %s\n", idx, stmts[idx]);
	next;
}

	{
	next;
}

END	{
	asort(stmts);
	for (i = 1; i <= idx; i++)
	{
		printf("%s\n", stmts[i]);
	}
	printf("\n");
	printf("\n");
	printf("\n");
	printf("popd                                                                            						2> /dev/null   > /dev/null\n");
	printf("\n");
}

