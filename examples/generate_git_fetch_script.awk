#
# GAWK script to extract URI definitions for github / gists repositories.
#


function extract_gist_number(uri, idx,          str, a, gist_nr) {
    # because MSys gawk doesn't support match() with 3 arguments :-((
    str = gensub(/^.*bl\.ocks\.org/, "xxx", 1, uri);
    str = gensub(/^.*gist\.github\.com/, "xxx", 1, str);

    split(str, a, "/");
    gist_nr = a[idx];
    # strip off any trailing cruft:
    gist_nr = gensub(/[^0-9].*$/, "", 1, gist_nr);
    #printf("Selecting Gist URI [%s] for line '%s' '%s'\n", gist_nr, uri, str);
    return gist_nr;
}

function extract_user_and_repo(uri, idx,          str, a, gist_nr) {
    str = gensub(/^.*github\.com/, "xxx", 1, uri);

    split(str, a, "/");
    user = a[idx];
    repo = clean_repo(a[idx + 1]);
    #printf("Selecting Github repo URI [%s/%s] for line '%s' '%s'\n", user, repo, uri, str);
    return user "/" repo;
}

function clean_repo(str,                            s) {
    for (;;) {
        s = gensub(/\.git$/, "", "g", str);
        if (s == str)
            return str;
        str = s;
    }
}

function mk_name(str) {
    str = gensub(/[^a-zA-Z0-9_-]+/, "_", "g", str);
    str = gensub(/_+/, "_", "g", str);
    return str;
}

function push_entry(idx, line) {
    stmts[idx] = line;
    #printf("# id %s: '%s' --> '%s'\n", idx, line, stmts[idx]);
}

BEGIN {
    printf("#! /bin/bash\n");
    printf("# generated by generate_git_fetch_script.sh\n");
    printf("\n");
    printf("pushd $(dirname $0)                                                                                     2> /dev/null   > /dev/null\n");
    printf("\n");
    printf("\n");
    printf("\n");
    printf("function git_add {\n");
    printf("    if test -d \"$2\" ; then\n");
    printf("        pushd .                                                                                         2> /dev/null   > /dev/null\n");
    printf("        cd $2\n");
    printf("        git pull --all\n");
    printf("        popd                                                                                            2> /dev/null   > /dev/null\n");
    printf("    else \n");
    printf("        git clone $1 $2\n");
    printf("    fi\n");
    printf("    if test \"$mode\" = \"W\" ; then\n");
    printf("        git_register_remote_for_UnixVM $1 $2\n");
    printf("    fi\n");
    printf("}\n");
    printf("\n");
    printf("function git_register_remote_for_UnixVM {\n");
    printf("    if test -d \"$2\" ; then\n");
    printf("        if test -d \"$Win7DEV_BASEDIR/$2\" ; then\n");
    printf("            pushd $2                                                                                    2> /dev/null  > /dev/null\n");
    printf("            git remote remove Win7DEV\n");
    printf("            git remote add Win7DEV $Win7DEV_BASEDIR/$2\n");
    printf("            popd                                                                                        2> /dev/null  > /dev/null\n");
    printf("        fi\n");
    printf("    fi\n");
    printf("}\n");
    printf("\n");
    printf("getopts \":Wh\" opt\n");
    printf("#echo opt+arg = \"$opt$OPTARG\"\n");
    printf("case \"$opt$OPTARG\" in\n");
    printf("W )\n");
    printf("  echo \"--- registering Win7DEV as remote ---\"\n");
    printf("  mode=\"W\"\n");
    printf("  for (( i=OPTIND; i > 1; i-- )) do\n");
    printf("    shift\n");
    printf("  done\n");
    printf("  #echo args: $@\n");
    printf("  if test -d \"$1\" ; then\n");
    printf("    Win7DEV_BASEDIR=$1\n");
    printf("  fi\n");
    printf("  ;;\n");
    printf("\n");
    printf("\"?\" )\n");
    printf("  echo \"--- registering D3 example git repositories ---\"\n");
    printf("  mode=\"R\"\n");
    printf("  ;;\n");
    printf("\n");
    printf("* )\n");
    printf("  cat <<EOT\n");
    printf("$0 [-W <optional_remote_path>]\n");
    printf("\n");
    printf("set up all D3 example git repositories.\n");
    printf("\n");
    printf("-W       : set up 'Win7DEV' remote reference per repository\n");
    printf("\n");
    printf("           When you don't specify the remote path yourself,\n");
    printf("           the default is set to:\n");
    printf("             \"$Win7DEV_BASEDIR\"\n");
    printf("\n");
    printf("EOT\n");
    printf("  exit\n");
    printf("  ;;\n");
    printf("esac\n");
    printf("\n");
    printf("\n");
    printf("\n");
    printf("\n");

    state = 0;
    idx = 0;
    stmts[0] = "";
}



# http://bl.ocks.org/4341417
/bl\.ocks\.org\/[0-9]+$/        {
    gist_nr = extract_gist_number($0, 2);

    full_uri = sprintf("https://gist.github.com/%s.git", gist_nr);
     
    push_entry("gist-" gist_nr, sprintf("git_add  %-70s  %s", full_uri, "gist-" gist_nr));
    next;
}

# http://bl.ocks.org/4710330/94a7c0aeb6f09d681dbfdd0e5150578e4935c6ae
/bl\.ocks\.org\/[0-9]+\//        {
    gist_nr = extract_gist_number($0, 2);

    full_uri = sprintf("https://gist.github.com/%s.git", gist_nr);
     
    push_entry("gist-" gist_nr, sprintf("git_add  %-70s  %s", full_uri, "gist-" gist_nr));
    next;
}

# http://bl.ocks.org/mbostock/1005090
/bl\.ocks\.org\/[^\/]+\/[0-9]+$/        {
    gist_nr = extract_gist_number($0, 3);

    full_uri = sprintf("https://gist.github.com/%s.git", gist_nr);
     
    push_entry("gist-" gist_nr, sprintf("git_add  %-70s  %s", full_uri, "gist-" gist_nr));
    next;
}

# http://bl.ocks.org/mbostock/1005090/
/bl\.ocks\.org\/[^\/]+\/[0-9]+\//        {
    gist_nr = extract_gist_number($0, 3);

    full_uri = sprintf("https://gist.github.com/%s.git", gist_nr);
     
    push_entry("gist-" gist_nr, sprintf("git_add  %-70s  %s", full_uri, "gist-" gist_nr));
    next;
}

# http://bl.ocks.org/ericcitaire/raw/5408146/thumbnail.png
/bl\.ocks\.org\/[^\/]+\/raw\/[0-9]+\//        {
    gist_nr = extract_gist_number($0, 4);

    full_uri = sprintf("https://gist.github.com/%s.git", gist_nr);
     
    push_entry("gist-" gist_nr, sprintf("git_add  %-70s  %s", full_uri, "gist-" gist_nr));
    next;
}

# gist.github.com/1005090.git
/gist\.github\.com\/[0-9]+\./        {
    gist_nr = extract_gist_number($0, 2);

    full_uri = sprintf("https://gist.github.com/%s.git", gist_nr);
     
    push_entry("gist-" gist_nr, sprintf("git_add  %-70s  %s", full_uri, "gist-" gist_nr));
    next;
}


# skip these buggers:
/(\/|\.)cloud.github.com\//        {
    next;
}
/(\/|\.)gist.github.com\//        {
    # all suitable ones have been handled above...
    next;
}
/\/github.com\/gist\//        {
    next;
}
/\/github.com\/wiki\//        {
    next;
}
/\/raw.github.com\/wiki\//        {
    next;
}


# https://raw.github.com/mlarocca/Dynamic-Charts/...
/\/raw.github.com\/[^\/]+\/[^\/]+/        {
    git_repo = extract_user_and_repo($0, 2);

    full_uri = sprintf("https://github.com/%s.git", git_repo);
     
    push_entry("github-" git_repo, sprintf("git_add  %-70s  %s", full_uri, mk_name("github-" git_repo)));
    next;
}

# http://latentflip.github.com/d3/techmeetup
/\/[^\/]+\.github\.com\/[^\/]+/        {
    str = gensub(/^.*\/([^\/]+)\.github\.com\/([^\/]+)(\/.*)?$/, "\\1 \\2", "g", $0);

    split(str, a, " ");
    user = a[1];
    repo = clean_repo(a[2]);
    #printf("Selecting Github repo URI [%s/%s] for line '%s' '%s'\n", user, repo, $0, str);
    git_repo = user "/" repo;

    full_uri = sprintf("https://github.com/%s.git", git_repo);
     
    push_entry("github-" git_repo, sprintf("git_add  %-70s  %s", full_uri, mk_name("github-" git_repo)));
    next;
}

# https://github.com/mlarocca/Dynamic-Charts/...
/\/github.com\/[^\/]+\/[^\/]+/        {
    git_repo = extract_user_and_repo($0, 2);

    full_uri = sprintf("https://github.com/%s.git", git_repo);
     
    push_entry("github-" git_repo, sprintf("git_add  %-70s  %s", full_uri, mk_name("github-" git_repo)));
    next;
}



            {
    next;
}

END             {
    # nuke a few obvious entries:
    stmts["github-mbostock/d3"] = "";

    max = asort(stmts);
    for (i = 1; i <= max; i++)
    {
        printf("%s\n", stmts[i]);
    }
    printf("\n");
    printf("\n");
    printf("\n");
    printf("popd                                                                                                    2> /dev/null   > /dev/null\n");
    printf("\n");
}

